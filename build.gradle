// INITIAL CONFIGS
plugins {
    id 'java'
    id 'war'
    id 'pmd'
    id "jacoco"
    id "org.gretty" version "3.0.3"
    id "org.kravemir.gradle.sass" version "1.2.4"
}

group = 'av2.webdev'
version = '0.0.1'

repositories {
    jcenter()
}

dependencies {
    providedCompile group: 'javax', name: 'javaee-web-api', version: '8.0.1'
    compile group: "javax.servlet", name: "jstl", version: "1.2"
    compile group: 'mysql', name: 'mysql-connector-java', version: '8.0.22'
    testImplementation group: 'junit', name: 'junit', version: '4.13.1'
}

// PLUGIN CONFIGS
gretty {
    httpPort = 3001
    contextPath = '/'
}

sass {
    dev {
        srcDir = file("$projectDir/src/scss")
        outDir = file("$projectDir/src/main/webapp")
        exclude = "**/_*.scss"
        description = 'Compile scss files into a single css file (development)'
    }
    prod {
        srcDir = file("$projectDir/src/scss")
        outDir = file("$projectDir/src/main/webapp")
        exclude = "**/_*.scss"
        minify = true
        description = 'Compile scss files into a single and minified css file (production)'
    }
}

jacoco {
    toolVersion = "0.8.6"
}

// TASK CONFIGS
defaultTasks 'development'

def printTask = {
    task -> println "Running ${task.description.toLowerCase()}"
}

task development {
    group 'Project Specific'
    description 'Development build task'
    dependsOn = ['check', 'devSass', 'tomcatRun']
    
    doFirst printTask
}

task production {
    group 'Project Specific'
    description 'Production build task'
    dependsOn = ['check', 'prodSass', 'war']
    
    doFirst printTask
}

test {
    finalizedBy jacocoTestReport 
}

jacocoTestReport {
    dependsOn test 
}

pmd {
    ignoreFailures = true
}

// OTHER CONFIGS
if (hasProperty('buildScan')) {
    buildScan {
        termsOfServiceUrl = 'https://gradle.com/terms-of-service'
        termsOfServiceAgree = 'yes'
    }
}
